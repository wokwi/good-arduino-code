---
layout: 'guide'
title: 'Charlieplexing Arduino: Control 72 LEDs with just 9 Arduino Pins - The complete guide'
shortTitle: 'Charlieplexing Arduino'
description: 'A step-by-step tutorial with annotated code examples, Schematics, illustrations to help you master Charlieplexing ü§ì'
thumbnail: '/images/guides/charlieplexing.svg'
slug: 'charlieplexing-complete-guide-72-leds-arduino'
---

import Img from 'react-optimized-image';
import { CharlieplexingMatrix } from '@/components/charlieplexing-matrix';

## Introduction

Blinking an LED üí° using an Arduino is the ‚ÄúHello World‚Äù of electronics.
There are many ways to blink an LED using Arduino - [5 Ways to Blink an LED with Arduino](https://blog.wokwi.com/5-ways-to-blink-an-led-with-arduino/) describes at least 5 ways of blinking an LED.

Let's take a look at a basic example: üëá

```arduino
void setup() {
  /* gac: LED_BUILTIN maps to Arduino Uno pin 13 */
  pinMode(LED_BUILTIN, OUTPUT);
}

void loop() {
  /* gac: Turns the LED on */
  digitalWrite(LED_BUILTIN, HIGH);
  delay(1000);
  /* gac: Turns the LED off */
  digitalWrite(LED_BUILTIN, LOW);
  delay(1000);
}
```

The code shown above will blink an LED that is connected to Arduino pin 13. But often we
need to drive more than one LED.

For example, if we are driving three LEDs for a traffic light üö• project, it is simple
and straightforward to use three Arduino port pins. But what happens if we want to use
a large number of LEDS, such as a 5x5 LED matrix?

Arduino only has 20 GPIO pins that we can use, so using one pin per LED won't do the
trick for us. That's a shame, because a 5x5 LED matrix can display numbers, symbols
and letters and could be a great addition to our next Arduino project.

Fortunately, there are other, more pin-efficient ways to connect LEDs to Arduino.
Charlieplexing is one of the less-known methods to solve this problem: it enables
controlling many LEDs with just a few Arduino pins!

You are going to learn how it works. By the end of this article, you will have a
clear understanding of how Charlieplexing works, as well as how to code and wire
the hardware with full confidence üòé.

## LED There be Light üí°

A standard LED has two connections: Anode (+) and Cathode (-). You can make it
emit light by applying positive voltage to the LED's Anode, and negative voltage
to the Cathode.

The amount voltage you need varies between LED of different colors and types,
and is called "forward voltage". For standard 5MM red LEDs, the forward voltage
will be between 1.6 volts to 2 volts.

The LEDs also have a certain current limit (usually around 20mA). Hence, you should
use a resistor to make sure only the desired amount of current flows through the
LED (or else, it may suffer and die!).

## Charlieplexing Makes it Possible?

With Charlieplexing, we can drive up to 6 LEDs using just three pins. How does that
work? In short, we're taking advantage of a limitation of the human eye: when
multiple LEDs are sequentially lit one after the other rapidly, it will appear as
all of them are lit at the same time.

This is called "[Persistence of Vision](https://en.wikipedia.org/wiki/Persistence_of_vision)"
(PoV in short).

Here are some popular examples of what you can create using this technique
(and Charlieplexing):

1. Your own matrix display driver
2. Rolling display board
3. Clock with 60 LEDs around the clock to tell the hour, minute and second hands
4. Chessboard which is bottom lit ‚Äì 64 LEDs
5. LED Cubes ‚Äì beatiful 3D cube made out of 8x8x8 LEDs (512 in total)

## How many LEDs can you drive with a given number of pins?

Short answer: many!

For an exact number, check this table out:

| Number of Pins | How many LEDs can you drive? |
| -------------- | ---------------------------- |
| 2              | 2                            |
| 3              | 6                            |
| 4              | 12                           |
| 5              | 20                           |
| 6              | 30                           |
| 7              | 42                           |
| 8              | 56                           |
| 9              | 72                           |
| **n**          | **n \* (n-1)**               |

In general, you can drive `n*(n-1)` LEDs using `n` pins. This is the number of ways
of selecting `2` items out of `n` different items. For more info about the math, take a
look at the [Wikipedia article](https://en.wikipedia.org/wiki/Charlieplexing#Traditional_multiplexing).

---

You are still here? Great, let's see how Charlieplexing is done in practice. We'll start
from a simple Arduino design with two-port pins and work our way up to nine-port pins!

The best way to learn is by doing. Hence, we will use [Wokwi's Online Arduino Simulator](https://wokwi.com/),
meaning you will be able to run all the examples in your browser.

Why not just build the circuit with real LEDs? Trust me, you don't want to sit for hours
checking the wiring for 72 LEDs and scratching your head, just to figure out that you
have plugged some LEDs in the wrong direction or have some loose wires.

When learning (and prototyping), I believe it's best to isolate the software from the
hardware. Thus, the simulation environment gives you a working hardware, so you can
focus on learning the concept and reduce the risk of issues.

Once you understood how Charlieplexing works and have the code running in the simulator
and doing once you want, then yes, I encourage you to go ahead and build the hardware.
This way, you can minimize frustration and maximize fun! üòÅ

As you follow the rest of the tutorial, you'll be able to run the code on Wokwi's
Arduino playground with simulated LEDs, verify the code, build confidence, and when
you are ready, also build the physical circuit with the tested-and-tried software.
Let's get going!

## Light'em Up!üèá

All our examples will follow this principle: the common goal will be to be able to
individually control each LED, turn it on for a moment, and then turn it off.
Eventually, code should cycle through all the LEDs.

### Case 1: Charlieplexing 2 LEDs with 2 Arduino pins

We'll start with the most basic example: just 2 LEDs.

The Cathode of the LED<sub>(2,3)</sub> is connected to Arduino pin 2 and Anode of the
LED is connected to Arduino pin 3.

Similarly, The Cathode of the LED<sub>(3,2)</sub> is connected to Arduino Pin 3 and
Anode of the LED is connected to Arduino Pin 2.


<figure>
  <CharlieplexingMatrix size={2} animate={false} />
  <figcaption>
    Charlieplexing 2 LEDs using two Arduino Pins
  </figcaption>
</figure>

#### Circuit diagram

<figure>
  <Img src={require('./charlieplexing/charlieplexing-using-arduino-basics.jpg')} />
  <figcaption>Charlieplexing using Arduino - Basics</figcaption>
</figure>


<figure>
  <Img src={require('./charlieplexing/arduino-pins-used-for-Charlieplexing.jpg')} />
  <figcaption>Arduino Pins used for Charlieplexing... in this article</figcaption>
</figure>


Here is what our code will be doing:

1. Set pin 2 as OUTPUT pin
2. Set pin 3 as OUTPUT pin
3. To turn LED(2,3) ON
   a. Drive logic LOW on pin 2
   b. Drive logic HIGH on pin 3
4. To turn LED(3,2) ON
   a. Drive logic HIGH on pin 2
   b. Drive logic LOW on pin 3
5. Repeat steps 3-4 endlessly


<figure>
  <Img src={require('./charlieplexing/port-pin-2-and-3.jpg')} />
  <figcaption>Port pin 2 and 3 status for driving two LEDs using two Arduino Pins</figcaption>
</figure>


**Tips**: The LEDs need to have a similar forward voltage drop so as to be able to
drive all of the LEDs with equal brightness. If you have LEDs of same color and type,
it should do the trick üòâ . Also, when building this circuit, you'll need one current
limiting resistor for each used Arduino pin.

You can have a look at the output of the simulation taken from
[Wokwi Arduino playground](https://wokwi.com/playground/charlieplexing):

<figure>
  <CharlieplexingMatrix size={2} speed={500} />
  <figcaption>
    Charlieplexing 2 LEDs using two Arduino Pins
  </figcaption>
</figure>

#### Arduino Code: Basic 2 LED control with Charlieplexing

This is the code we ran in the simulation. The green markers explain more
about the code - move your mouse over those lines (on tap them) to see
the extra information:

<figure>

```arduino
/* Charlieplexing example: 2 pins driving 2 LEDs. */

/* gac: SCAN DELAY is the amount of time each LED will be ON */
#define SCAN_DELAY 500

void setup() {
}

void loop() {
  /* gac: We have to configure the pins as `OUTPUT` before we can drive
     them `HIGH` or `LOW` */
  pinMode(2, OUTPUT);
  pinMode(3, OUTPUT);
  /* gac: orward bias the LED(2,3), turning it ON. */
  digitalWrite(2, LOW);
  digitalWrite(3, HIGH);

  // Keep LED on for SCAN_DELAY microseconds:
  delay(SCAN_DELAY);

  /* gac:
    Configure Arduino pins 2, 3 as `INPUT`.

    This causes the LED to turn off since both pin will be at high impedance when
    configured as input, and no current will flow through the LEDs. */
  pinMode(2, INPUT);
  pinMode(3, INPUT);

  /* gac: Turn on LED(3,2) in a similar fashion */
  pinMode(2, OUTPUT);
  pinMode(3, OUTPUT);
  digitalWrite(2, HIGH);
  digitalWrite(3, LOW);
  delay(SCAN_DELAY);
  /* gac: Set pins to `INPUT` to turn the LEDs off */
  pinMode(2, INPUT);
  pinMode(3, INPUT);
}
```
<figcaption>Charlieplexing - direct implementation</figcaption>
</figure>

But wait! What's the benefit of this code? You could have connected two Arduino Pins to two LEDs
and drive them directly, and the code would be much simpler.

In reality, you won't be using Charlieplexing when you just need to drive 2 LEDs. It doesn't make sense.
But I included this example to explain the basic logic behind Charlieplexing. Let's see how we can
generalize this code so that it works for any number of pins:

#### Code Summary: Generalizing Charlieplexing!

We looked at 2 specific examples above:

- To turn on LED(2,3), we set pin 2 LOW and pin 3 HIGH
- To turn on LED(3,2), we set pin 3 LOW and pin 2 HIGH

Now, let's look at the generalized version of how we can control Charlieplexed LEDs:

1. Set all pins to `INPUT`.
2. Turning on LED(x,y):
   - Configure pins x and y as `OUTPUT`.
   - Drive pin x `LOW` and pin y `HIGH`.
3. Turning off LED(x,y): configure pins x and y as `INPUT`.

In the previous code example, we tried to keep the code simple and easy to understand.
The downside is that you cannot eaisly expand the code for three (or more) pins.

So now, we're going to rewrite the code. It'll still do the same, but make it easier
for us to extend later:

```
/* Charlieplexing example: 2 pins driving 2 LEDs with loops. */

#define SCAN_DELAY 500

/* gac: The port pins are defined as an array. In this case it will be two port pins: 2, 3 */
byte LEDS[] = { 2, 3 };
/* gac: to derive the number of port pins. LED_COUNT will get a value of 2 */
const byte LED_COUNT = sizeof(LEDS);

void setup() {
}

void loop() {
  /* gac:
    Variables `i` and `j` represent port pins of the Arduino.
    In first iteration, <br />
    `i` will have value of 0 --> LEDS[i] will be 2 (first element in the array) <br />
    `j` will have value of 0 --> LEDS[j] will be 2.

    `if` condition will fail.

    In the second iteration of inner for-loop, LEDS[j] will be 3.
    The `if` condition will pass.

    Since, there are only two port pins, go through the iterations manually to see the logic by yourself.
    you can leave a comment if there is need for more clarity
  */
  for (int i = 0; i < LED_COUNT; i++) {
    for (int j = 0; j < LED_COUNT; j++) {
      if (i != j) { //The LED port pins are always connected to two different pins
        pinMode(LEDS[i], OUTPUT); //port 2
        pinMode(LEDS[j], OUTPUT); //port 3
        /* gac: writing port pins with the below sequence, will turn on LED(i,j)
          In first iteration, the LED(2,3) will be turned on.
          In the second iteration, LED(3,2) will be turned on.
        */
        digitalWrite(LEDS[i], LOW);
        digitalWrite(LEDS[j], HIGH);
        delay(SCAN_DELAY);
        /* gac: We will always take the pins to high impedance state before driving the next pins */
        pinMode(LEDS[i], INPUT);
        pinMode(LEDS[j], INPUT);
      }
    }
  }
}
```

### Case 2: Driving six LEDs with three Arduino Pins-Charlieplexing

3 pins ‚áí 3 \* 2 = 6 LEDs.

#### Circuit Diagram

<figure>
  <Img src={require('./charlieplexing/driving-6-leds-with-3-arduino-pins.jpg')} />
  <figcaption>How to drive 6 LEDs using 3 Arduino port pins</figcaption>
</figure>

And the 6 LEDs arranged as follows:

<figure>
  <CharlieplexingMatrix size={3} animate={false} />
  <figcaption>
    Driving 6 LEDs using 3 Arduino pins - Charlieplexing
  </figcaption>
</figure>

#### Arduino code

<figure>

```
/* Charlieplexing example: 3 pins driving 6 LEDS. */

#define SCAN_DELAY 500

/* gac: Life is simple now - we can support as many pins as we want by adding values to the `LEDS` array */
byte LEDS[] = { 2, 3, 4 };
const byte LED_COUNT = sizeof(LEDS);

void setup() {
}

void loop() {
  for (int i = 0; i < LED_COUNT; i++) {
    for (int j = 0; j < LED_COUNT; j++) {
      if (i != j) {
        pinMode(LEDS[i], OUTPUT);
        pinMode(LEDS[j], OUTPUT);
        digitalWrite(LEDS[i], LOW);
        digitalWrite(LEDS[j], HIGH);
        delay(SCAN_DELAY);
        pinMode(LEDS[i], INPUT);
        pinMode(LEDS[j], INPUT);
      }
    }
  }
}
```
 <figcaption>How to drive 6 LEDs using 3 Arduino Port pins - Charlieplexing</figcaption>
</figure>


<figure>
  <CharlieplexingMatrix size={3} speed={500} />
  <figcaption>
    Simulation output - Charlieplexing 6 LEDs using 3 Arduino Pins
  </figcaption>
</figure>

#### Code Summary

Since we made our code modular, we only had to change a single place: add the
new pin number (4) to the `LEDS[]` array.

### Case 3: Driving twelve LEDs with four Arduino Pins-Charlieplexing

4 pins ‚áí 4 \* 3 = 12 LEDs.

#### Circuit Diagram

<figure>
  <Img src={require('./charlieplexing/4_pins_12_leds_control.jpg')} />
  <figcaption>How to drive 12 LEDs using 4 Arduino Pins - Charlieplexing</figcaption>
</figure>


You have 12 LEDs in this case

¬∑ LED(2,3), LED(2,4), LED(2,5),

¬∑ LED(3,2), LED(3,4),LED(3,5),

¬∑ LED(4,2), LED(4,3), LED(4,5),

¬∑ LED(5,2), LED(5,3), LED(5,4)

#### Arduino code

<figure>

```
/* Charlieplexing example: 12 pins driving 4 LEDS. */

#define SCAN_DELAY 500

byte LEDS[] = { 2, 3, 4, 5 };
const byte LED_COUNT = sizeof(LEDS);

void setup() {
}

void loop() {
  for (int i = 0; i < LED_COUNT; i++) {
    for (int j = 0; j < LED_COUNT; j++) {
      if (i != j) {
        pinMode(LEDS[i], OUTPUT);
        pinMode(LEDS[j], OUTPUT);
        digitalWrite(LEDS[i], LOW);
        digitalWrite(LEDS[j], HIGH);
        delay(SCAN_DELAY);
        pinMode(LEDS[i], INPUT);
        pinMode(LEDS[j], INPUT);
      }
    }
  }
}
```
 <figcaption>How to drive 12 LEDs using 4 Arduino Pins - Charlieplexing!</figcaption>
</figure>

<figure>
  <CharlieplexingMatrix size={4} speed={500} />
  <figcaption>
    Charlieplexing 12 LEDs using 4 Arduino Pins
  </figcaption>
</figure>

### Case 4: Driving 20 LEDs with five Arduino Pins-Charlieplexing

5 pins ‚áí 5 \* 4 = 20 LEDs. Wiring starts to get complex, but the code will still be
pretty much the same.

#### Circuit Diagram

<figure>
  <Img src={require('./charlieplexing/20-leds-with-5.png')} />
  <figcaption>How to drive 20 LEDs using 5 Arduino Pins - Charlieplexing</figcaption>
</figure>

You have 20 LEDs in this case:

<figure>
  <CharlieplexingMatrix size={5} animate={false} />
  <figcaption>
    Driving 20 LEDs using 5 Arduino Pins using Charlieplexing
  </figcaption>
</figure>


#### Arduino code

<figure>

```
/* Charlieplexing example: 5 pins driving 20 LEDS. */

#define SCAN_DELAY 500

byte LEDS[] = { 2, 3, 4, 5, 6 };
const byte LED_COUNT = sizeof(LEDS);

void setup() {
}

void loop() {
  for (int i = 0; i < LED_COUNT; i++) {
    for (int j = 0; j < LED_COUNT; j++) {
      if (i != j) {
        pinMode(LEDS[i], OUTPUT);
        pinMode(LEDS[j], OUTPUT);
        digitalWrite(LEDS[i], LOW);
        digitalWrite(LEDS[j], HIGH);
        delay(SCAN_DELAY);
        pinMode(LEDS[i], INPUT);
        pinMode(LEDS[j], INPUT);
      }
    }
  }
}
```
  <figcaption>How to drive 20 LEDs using 5 Arduino Pins - Charlieplexing!</figcaption>
</figure>

<figure>
  <CharlieplexingMatrix size={5} speed={500} />
  <figcaption>
    Simulation output - Charlieplexing 20 LEDs using 5 Arduino Pins
  </figcaption>
</figure>


### Case 5: Driving 30 LEDs with six Arduino Pins-Charlieplexing

6 pins ‚áí 6 \* 5 = 30 LEDs. Just one more pin, and we can control
10 more LEDs. Isn't that fun?

#### Circuit Diagram

<figure>
  <Img src={require('./charlieplexing/30-leds-with-6-pins.png')} />
  <figcaption>How to drive 30 LEDs using 6 Arduino pins - Charlieplexing</figcaption>
</figure>

And the LEDs:

<figure>
  <CharlieplexingMatrix size={6} speed={500} />
  <figcaption>
    Driving 30 LEDs using 6 Arduino Pins - Charlieplexing
  </figcaption>
</figure>

The code was omitted for brevity. We basically add the number `7` at the end of the
`LEDS` array.

### Case 6: Driving 42 LEDs with 7 Arduino Pins-Charlieplexing

7 pins ‚áí 7 \* 6 = 42 LEDs. So with just 7 pins, we can convey the
meaning of the universe in LEDs! üåé

#### Circuit Diagram

<figure>
  <Img src={require('./charlieplexing/42-leds-with-7.png')} />
  <figcaption>Driving 42 LEDs using 7 Arduino pins - Charlieplexing</figcaption>
</figure>

One universe, 42 LEDs. Please don't forget your towel!

<figure>
  <CharlieplexingMatrix size={7} speed={500} />
  <figcaption>
    Driving 42 LEDs using 7 Arduino Pins - Charlieplexing
  </figcaption>
</figure>

We will get back to discussing the code shortly.

### Case 7: Driving 56 LEDs with 8 Arduino Pins-Charlieplexing

8 pins ‚áí 8 \* 7 = 56 LEDs. And we're not even done - there's one more
pin down the road.

#### Circuit Diagram

<figure>
  <Img src={require('./charlieplexing/56-leds-with-8-pins.png')} />
  <figcaption>How to drive 56 LEDs using 8 Arduino Pins - Charlieplexing</figcaption>
</figure>

56 LEDs for your pleasure below. We made the animation a bit faster now, otherwise
it'd be to boring ;-)

<figure>
  <CharlieplexingMatrix size={8} speed={100} />
  <figcaption>
    Driving 56 LEDs using 8 Arduino Pins - Charlieplexing
  </figcaption>
</figure>

And if you want to try it yourself, just head over to our [Charlieplexing playground](https://wokwi.com/playground/charlieplexing)
and give it a spin! Experimenting yourself is key to learning.

### Case 8: Driving 72 LEDs with 9 Arduino Pins-Charlieplexing

You've made it to the last example! Actually, I was too lazy to draw the connection diagram here
(it gets very tedious as the number grows). But I'm pretty sure at this point you got the idea...

You have 72 LEDs in this case:

<figure>
  <CharlieplexingMatrix size={9} speed={5} />
  <figcaption>
    Driving 72 LEDs using 9 Arduino Pins - Charlieplexing
  </figcaption>
</figure>

#### Arduino code

<figure>

```
/* Charlieplexing example: 9 pins driving 72 LEDs. */

#define SCAN_DELAY 5

byte LEDS[] = { 2, 3, 4, 5, 6, 7, 8, 9, 10 };
const byte LED_COUNT = sizeof(LEDS);

void setup() {
}

void loop() {
  for (int i = 0; i < LED_COUNT; i++) {
    for (int j = 0; j < LED_COUNT; j++) {
      if (i != j) {
        pinMode(LEDS[i], OUTPUT);
        pinMode(LEDS[j], OUTPUT);
        digitalWrite(LEDS[i], LOW);
        digitalWrite(LEDS[j], HIGH);
        delay(SCAN_DELAY);
        pinMode(LEDS[i], INPUT);
        pinMode(LEDS[j], INPUT);
      }
    }
  }
}
```
  <figcaption>How to drive 72 LEDs using 9 Arduino Pins - Charlieplexing</figcaption>
</figure>

## When to use Charlieplexing ?

1. Charlieplexing is best suited where you need to drive only a few LEDs at a time
2. You can display almost any pattern by quickly turning the LEDs on and off, but your MCU
   will be working hard.
2. The brightness of the LED may go down or become uneven when we are driving many LEDs at the same time
3. If the leakage current of the MCU is high, the LEDs might still be lit (poorly, but still visible in the dark)
   when the pins are configured as input. Also, beware of enabling the built-in Pull-up / Pull-down resistors!
4. Debugging the hardware and wiring gets really clumsy as the number of LEDs grow
5. Instead of Charlieplexing, you can use external shift registers/multiplexers. The code will be simpler,
   the connections will be more straightforward, but you'd need these extra hardware parts.

That was it! If you have any questions, feel free to [drop me an email](mailto:uri+charlieplexing@wokwi.com).

